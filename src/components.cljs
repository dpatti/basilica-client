(ns basilica.components
  (:require-macros [cljs.core.async.macros :refer [go]])
  (:require
   [om.core :as om :include-macros true]
   [om.dom :as dom :include-macros true]
   [clojure.string :as string]
   [clojure.set :refer [select]]
   [cljs.core.async :as async :refer [chan close! put!]]))

(defn link-button [on-click text]
  (dom/a #js {:href "#"
              :className "button"
              :onClick #(do (on-click) false)}
         text))

(defn format [timestamp-string]
  (.fromNow (js/moment timestamp-string)))

(defn with-classes [keys & all]
  (clj->js (into keys {:className (string/join " " all)})))

(defn classes [& all]
  (apply with-classes {} all))

(defn bump-height [el delta]
  (let [height (.-clientHeight el)]
    (set! (.. el -style -height) (str (+ height delta) "px"))))

(defn key-down [on-submit]
  (fn [e]
    (let [node (. e -target)
          key (. e -which)
          command (or (. e -metaKey) (. e -ctrlKey))
          bump-amount 40]
      (when command
        (when (= key 74) (bump-height node bump-amount))
        (when (= key 75) (bump-height node (- bump-amount)))
        (when (= key 13)
          (on-submit (. node -value))
          (set! (. node -value) ""))))))

(defn comment-component [on-submit owner]
  (reify
    om/IDidMount
    (did-mount [_] (.focus (om/get-node owner "input")))
    om/IRender
    (render
     [_]
     (dom/div (classes "comment")
              "enter: newline, cmd-enter: submit, cmd-j/k: resize | markdown coming eventually"
              (dom/textarea #js {:ref "input", :onKeyDown (key-down on-submit)})))))

(defn thread-header-component [thread]
  (dom/div (classes "header")
           (thread :by)
           " "
           (format (thread :at))
           " "
           (dom/a #js {:href (str "/" (thread :id))} "link")))

(defn thread-body-component [on-click thread]
  (dom/div (classes "content")
           (thread :content)
           " "
           (let [child-count (thread :count)
                 text (if (= child-count 0) "comment" (str child-count " comments" ))]
             (link-button (partial on-click thread) text))))

(defn thread-children-component [on-comment build-child threads]
  (apply dom/div (classes "children")
         (om/build comment-component on-comment)
         (map build-child threads)))

(defn thread-component [comment-ch on-click expanded id-thread threads owner]
  (reify
    om/IInitState
    (init-state [_] {:expanded-children #{}})
    om/IRenderState
    (render-state
     [_ {:keys [expanded-children]}]
     (let [thread (->> threads
                       (select #(= (% :id) id-thread))
                       first)
           children (->> threads
                         (select #(= (% :idParent) id-thread))
                         (sort-by :id >))]
       (dom/div (with-classes {:key id-thread} "thread" (if expanded "expanded" "collapsed"))
                (thread-header-component thread)
                (thread-body-component on-click thread)
                (if expanded
                  (let [click (fn [op] (fn [thread]
                                         (om/update-state! owner
                                                           :expanded-children
                                                           #(op % (thread :id)))))
                        build-child (fn [{id-child :id}]
                                      (om/build (if (contains? expanded-children id-child)
                                                  (partial thread-component comment-ch (click disj) true id-child)
                                                  (partial thread-component comment-ch (click conj) false id-child))
                                                threads))]
                    (thread-children-component #(put! comment-ch {:thread thread, :text %})
                                               build-child
                                               children)
                    ))))
     )))
